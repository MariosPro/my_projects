QT       += core gui opengl

greaterThan(QT_MAJOR_VERSION, 4): QT += widgets

TARGET = FaceDetection
TEMPLATE = app

# Note: to be changed according to your OpenCV configuration
INCLUDEPATH += /usr/local/opencv2/

SOURCES += main.cpp\
        cmainwindow.cpp \
    cqtopencvviewergl.cpp

HEADERS  += cmainwindow.h \
    cqtopencvviewergl.h

FORMS    += cmainwindow.ui


win32 {
message("Using win32 configuration")

OPENCV_PATH = C:/devel/OpenCV/opencv-2.4.6.0


#LIBS_PATH = "$$OPENCV_PATH/build/x86/mingw/lib" #project compiled using MINGW
LIBS_PATH = "$$OPENCV_PATH/build/x86/vc10/lib" #project compiled using Visual C++ 2010 32bit compiler

    CONFIG(debug, debug|release) {
    LIBS     += -L$$LIBS_PATH \
                -lopencv_core246d \
                -lopencv_highgui246d \
                -lopencv_objdetect246d
    }

    CONFIG(release, debug|release) {
    LIBS     += -L$$LIBS_PATH \
                -lopencv_core246 \
                -lopencv_highgui246 \
                -lopencv_objdetect246
    }

}

unix {
message("Using unix configuration")

OPENCV_PATH = /usr/local/opencv2/

LIBS     += -L$$LIBS_PATH \
            -lopencv_core.so.2.3 \
            -lopencv_highgui \
            -lopencv_objdetect
}

INCLUDEPATH += $$OPENCV_PATH/modules/core/include/ \ #core module
    $$OPENCV_PATH/modules/highgui/include/ \ #highgui module
    $$OPENCV_PATH/modules/objdetect/include/ #obj_detect module

message("OpenCV path: $$OPENCV_PATH")
message("Includes path: $$INCLUDEPATH")
message("Libraries: $$LIBS")
